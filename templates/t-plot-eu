set terminal pngcairo dashed font "Helvetica,13" size 800,600
set output "../graphs/covid-eu.png"
set logscale y
set key at -1,1000 box
set yrange [100:100000]
set xrange [-30:0]
set ylabel "Confirmed SARS-CoV-2 cases"
set xlabel "Days (from DATE March 2020)"
set mxtics
set grid mxtics xtics ytics mytics
offset = OFFSET
esoff = 6
froff = 8.5
deoff = 9
nloff = 16
ukoff = 15
plot "../country_data/italy" using ($0 +5 - offset):2 w l lw 2 t "Italy", \
"../country_data/spain" using ($0 + 10 - esoff  - offset):2 w l lw 2 lc "orange" t sprintf("Spain (%d days behind Italy)", esoff), \
"../country_data/france" using ($0 + 10 - froff - offset):2 w l lw 2 lc "red" t sprintf("France (%d days behind Italy)", froff), \
"../country_data/germany" using ($0 + 9 - deoff - offset):2 w l lw 2 lc "#00b000" t sprintf("Germany (%d days behind Italy)", deoff), \
"../country_data/netherlands" using ($0 + 12 - nloff -offset):2 w l lw 2 lc "#a0a0a0" t sprintf("Netherlands (%d days behind Italy)", nloff), \
\
"../country_data/uk" using ($0 +12 - ukoff - offset):2 w l lw 2 lc "blue" t sprintf("UK (%d days behind Italy)", ukoff), \
1.35**(x+offset + 9.5) lc "black"  dt 4 t "35% daily increase",\
1.22**(x+offset + 22.5) lc "black" dashtype 2 t "22% daily increase",\
1.135**(x+offset + 50.8) lc "black" dashtype 5 t "13.5% daily increase"

